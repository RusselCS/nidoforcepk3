actor NidoAlphaWep : SwordBase
{
weapon.ammotype "CapacityPowerA"
inventory.icon "WICOA"
States
{
Ready:
TNT1 A 104 A_WeaponReady // faster CP regeneration
TNT1 A 1 A_GiveInventory("CapacityPowerA", 1)
Goto Ready
// Fire and FireAttack inherit from SwordBase
AltFire: // HP/CP checks after stopping Nido
TNT1 A 0 A_JumpIfInventory("CapacityPowerA",1,"AltCont")
goto NoAmmo
AltCont:
TNT1 A 0 A_GunFlash // controls alt stopping
TNT1 A 0 A_TakeInventory("CapacityPowerA", 1)
TNT1 A 0 A_JumpIfHealthLower(3, "WeakAlt")
TNT1 A 0 A_PlaySoundEx("weapon/firethrow","Weapon")  // four fireballs when healthy
TNT1 A 1 A_SpawnItemEx("AtkFireball", 8, 0, 32, 20, 0, 7)
TNT1 A 0 A_SpawnItemEx("AtkFireball", 8, 0, 32, 17, 0, 7, 12)
TNT1 A 1 A_SpawnItemEx("AtkFireball", 8, 0, 32, 17, 0, 7, -12)
TNT1 A 0 A_SpawnItemEx("AtkFireball", 8, 0, 32, 13, 0, 7)
TNT1 A 12 // slightly slower attack speed when healthy
TNT1 A 0 A_Refire
Goto Ready
WeakAlt:
TNT1 A 0 A_PlaySoundEx("weapon/firethrow","Weapon")
TNT1 A 0 A_SpawnItemEx("AtkFireball", 8, 0, 32, 18, 0, 7)
TNT1 A 8
TNT1 A 0 A_Refire
Goto Ready
AltHold:
TNT1 A 1
TNT1 A 0 A_Refire
Goto Ready
NoAmmo:
TNT1 A 1
Goto Ready
// Flash state moved to SwordBase
}
}

actor CapacityPowerA : Ammo
{
inventory.amount 1
inventory.maxamount 5
}

actor AtkFireball
{
PROJECTILE
seesound "weapon/firebounce"
Radius 8
Height 16
scale 4.0
-NOGRAVITY
+CANBOUNCEWATER
+HEXENBOUNCE
+FORCEXYBILLBOARD
speed 20
bouncefactor 0.85
wallbouncefactor 0.65
reactiontime 14
damage (1)
States
{
Spawn:
ATFI ABC 2
BallBounce:
ATFI DEF 2
TNT1 A 0 A_Countdown
Goto BallBounce
Death:
TNT1 A 0 A_PlaySoundEX("weapon/firefizzle","Weapon")
TNT1 A 0 A_SpawnItemEx("GfxFirePart", 0, 0, 0, random(-8, 8), random(-8, 8), random(1, 4), random(0, 360))
TNT1 A 0 A_SpawnItemEx("GfxFirePart", 0, 0, 0, random(-8, 8), random(-8, 8), random(1, 4), random(0, 360))
TNT1 A 0 A_SpawnItemEx("GfxFirePart", 0, 0, 0, random(-8, 8), random(-8, 8), random(1, 4), random(0, 360))
TNT1 A 0 A_SpawnItemEx("GfxFirePart", 0, 0, 0, random(-8, 8), random(-8, 8), random(1, 4), random(0, 360))
TNT1 A 1
stop
}
}

actor GfxFirePart : GfxSword
{
damage (0)
States
{
Spawn:
ATFI GHJGHJ 3
Death:
TNT1 A 0
TNT1 A 1
stop
}
}