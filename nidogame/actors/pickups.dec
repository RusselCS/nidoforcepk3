// [Lego] DoomEd Numbers 11000 - 11999 is pickups.
// I don't think we'll need more than 20 for pickups, but hey.

actor ForceFragPickupFirst : CustomInventory 11000
{
//$Category NidoForce-Pickups
+FLOATBOB
+RANDOMIZE
+FORCEYBILLBOARD
Height 56
Scale 4.0
States
{
Spawn:
FCFG A 0
FCFG A 0 A_SetTics(random(0, 3))
FCFG AAA 4
Goto SpawnLoop
SpawnLoop:
FCFG AAAAAAAAAAAAAAAAA 4 A_SpawnItemEx("ForceFragLine", frandom(-20, 20), frandom(-20, 20), 0, 0, 0, 10)
FCFG BC 2
FCFG A 0 A_SpawnItemEx("ForceFragLine", frandom(-20, 20), frandom(-20, 20), 0, 0, 0, 10)
FCFG DA 2
FCFG A 0 A_SpawnItemEx("ForceFragLine", frandom(-20, 20), frandom(-20, 20), 0, 0, 0, 10)
loop
Pickup:
FFRG A 0 ACS_ExecuteAlways(11, 0, 0)
FFRG AAAAAAAAAA 0 A_SpawnItemEx("ForceFragPop", 0, 0, 0, 5, 0, random(5, 10), frandom(0, 360))
stop
}
}

actor ForceFragPickupSecond : ForceFragPickupFirst 11001
{
//$Category NidoForce-Pickups
States
{
Pickup:
FCFG A 0 ACS_ExecuteAlways(11, 0, 1)
FCFG AAAAAAAAAA 0 A_SpawnItemEx("ForceFragPop", 0, 0, 0, 5, 0, random(5, 10), frandom(0, 360))
stop
}
}

actor ForceFragPickupThird : ForceFragPickupFirst 11002
{
//$Category NidoForce-Pickups
States
{
Pickup:
FCFG A 0 ACS_ExecuteAlways(11, 0, 2)
FCFG AAAAAAAAAA 0 A_SpawnItemEx("ForceFragPop", 0, 0, 0, 5, 0, random(5, 10), frandom(0, 360))
stop
}
}

actor ForceFragBoss : ForceFragPickupFirst
{
Alpha 0.0
States
{
Spawn:
FCFG AA 0 A_SetTranslucent(0.0, 0)
FCFG A 0 A_SpawnItemEx("ForceFragLine", frandom(-20, 20), frandom(-20, 20), 0, 0, 0, 10)
FCFG AA 1 A_FadeIn
FCFG A 0 A_SpawnItemEx("ForceFragLine", frandom(-20, 20), frandom(-20, 20), 0, 0, 0, 10)
FCFG AA 1 A_FadeIn
FCFG A 0 A_SpawnItemEx("ForceFragLine", frandom(-20, 20), frandom(-20, 20), 0, 0, 0, 10)
FCFG AA 1 A_FadeIn
FCFG A 0 A_SpawnItemEx("ForceFragLine", frandom(-20, 20), frandom(-20, 20), 0, 0, 0, 10)
FCFG AA 1 A_FadeIn
FCFG A 0 A_SpawnItemEx("ForceFragLine", frandom(-20, 20), frandom(-20, 20), 0, 0, 0, 10)
FCFG AA 1 A_FadeIn
FCFG A 0 A_SetTranslucent(1.0, 0)
Goto Super::SpawnLoop
}
}

actor FakeForceFrag : CustomInventory
{
+RANDOMIZE
+FORCEYBILLBOARD
Renderstyle fuzzy
Scale 4.0
States
{
Spawn:
FCFG A 0
FCFG A 0 A_SetTics(random(0, 3))
FCFG AAA 4
Goto SpawnLoop
SpawnLoop:
FCFG AAAAAAAAAAAAAAAAA 4
FCFG BCDA 2
loop
Pickup:
FFRG AAAAAAAAAA 0 A_SpawnItemEx("ForceFragPop", 0, 0, 0, 5, 0, random(5, 10), frandom(0, 360))
stop
}
}

actor ForceFragLine
{
+NOINTERACTION
+NOGRAVITY
+FORCEYBILLBOARD
Reactiontime 10
scale 3.0
States
{
Spawn:
FCFG E 1 A_CountDown
loop
}
}

actor ForceFragPop : ForceFragLine
{
States
{
Spawn:
FCFG F 1 A_CountDown
loop
}
}

actor ForceFragStock : Inventory
{
inventory.amount 1
inventory.maxamount 999
}

actor ForceStock1 : Once {}
actor ForceStock2 : Once {}
actor ForceStock3 : Once {}

actor ForceStock1Frame1 : PowerUp {Powerup.duration 2}
actor ForceStock1Frame2 : PowerUp {Powerup.duration 4}
actor ForceStock1Frame3 : PowerUp {Powerup.duration 6}
actor ForceStock1Frame4 : PowerUp {Powerup.duration 8}

actor ForceStock2Frame1 : ForceStock1Frame1 {}
actor ForceStock2Frame2 : ForceStock1Frame2 {}
actor ForceStock2Frame3 : ForceStock1Frame3 {}
actor ForceStock2Frame4 : ForceStock1Frame4 {}

actor ForceStock3Frame1 : ForceStock1Frame1 {}
actor ForceStock3Frame2 : ForceStock1Frame2 {}
actor ForceStock3Frame3 : ForceStock1Frame3 {}
actor ForceStock3Frame4 : ForceStock1Frame4 {}

actor ForceStock1Anim : CustomInventory
{
States
{
Pickup:
TNT1 A 0 A_GiveInventory("ForceStock1Frame1", 1)
TNT1 A 0 A_GiveInventory("ForceStock1Frame2", 1)
TNT1 A 0 A_GiveInventory("ForceStock1Frame3", 1)
TNT1 A 0 A_GiveInventory("ForceStock1Frame4", 1)
stop
}
}

actor ForceStock2Anim : CustomInventory
{
States
{
Pickup:
TNT1 A 0 A_GiveInventory("ForceStock2Frame1", 1)
TNT1 A 0 A_GiveInventory("ForceStock2Frame2", 1)
TNT1 A 0 A_GiveInventory("ForceStock2Frame3", 1)
TNT1 A 0 A_GiveInventory("ForceStock2Frame4", 1)
stop
}
}

actor ForceStock3Anim : CustomInventory
{
States
{
Pickup:
TNT1 A 0 A_GiveInventory("ForceStock3Frame1", 1)
TNT1 A 0 A_GiveInventory("ForceStock3Frame2", 1)
TNT1 A 0 A_GiveInventory("ForceStock3Frame3", 1)
TNT1 A 0 A_GiveInventory("ForceStock3Frame4", 1)
stop
}
}

actor InHubMap : Once {}

actor ForceFrag1 : ForceFragStock {}
actor ForceFrag2 : ForceFragStock {}
actor ForceFrag3 : ForceFragStock {}
actor ForceFrag4 : ForceFragStock {}
actor ForceFrag5 : ForceFragStock {}
actor ForceFrag6 : ForceFragStock {}

actor Iggy1Frag : Once {}
actor Iggy2Frag : Once {}
actor Iggy3Frag : Once {}

actor CurrentLevel : Counter {}

actor HPPickup : CustomInventory 11003
{
//$Category NidoForce-Pickups
Inventory.pickupmessage "+1 HP"
Inventory.pickupsound "pickup/health"
Scale 4.0
States
{
Spawn:
HPUP ABCD 4
loop
Pickup:
HPUP A 0 A_JumpIf(ACS_ExecuteWithResult(NIDO_STATUS, STATUS_HEALCHECK)==TRUE, "Success")
fail
Success:
HPUP A 0 ACS_ExecuteWithResult(NIDO_STATUS, STATUS_HEAL)
stop
}
}

actor CPPickup : CustomInventory 11004
{
//$Category NidoForce-Pickups
Inventory.pickupmessage "+1 CP"
Inventory.pickupsound "pickup/capacity"
Scale 4.0
States
{
Spawn:
CPUP ABCD 4
loop
Pickup:
CPUP A 0
fail
}
}

//ACS_ExecuteAlways(NIDO_STATUS, 0, STATUS_MAXHEALTH, 6)

actor AlphaPickup : CustomInventory 11010
{
//$Category NidoForce-Weapons
Inventory.pickupmessage "Alpha ~ Flameshot"
Inventory.pickupsound "pickup/weapon"
Scale 4.0
States
{
Spawn:
ABUP A 1
loop
Pickup:
TNT1 A 0 A_JumpIfInventory("NidoAlphaWep", 1, "End")
TNT1 A 0 A_GiveInventory("NidoAlphaWep", 1)
TNT1 A 0 A_SelectWeapon("NidoAlphaWep")
TNT1 A 0 ACS_ExecuteAlways(NIDO_WEAPON, 0, 0)
TNT1 A 0 ACS_ExecuteAlways(NIDO_STATUS, 0, STATUS_MAXHEALTH, 5)// Remove Omega Buff
TNT1 A 0 A_TakeInventory("NidoFloatJump", 1)// Remove Beta Floating
TNT1 A 0 A_TakeInventory("NidoOmegaWep", 1)
TNT1 A 0 A_TakeInventory("NidoBetaWep", 1)
stop
End:
TNT1 A 0
fail
}
}

actor BetaPickup : CustomInventory 11011
{
//$Category NidoForce-Weapons
Inventory.pickupmessage "Beta ~ Cyrospear"
Inventory.pickupsound "pickup/weapon"
Scale 4.0
States
{
Spawn:
ABUP C 1
loop
Pickup:
TNT1 A 0 A_JumpIfInventory("NidoBetaWep", 1, "End")
TNT1 A 0 A_GiveInventory("NidoBetaWep", 1)
TNT1 A 0 A_SelectWeapon("NidoBetaWep")
TNT1 A 0 ACS_ExecuteAlways(NIDO_WEAPON, 0, 1)
TNT1 A 0 ACS_ExecuteAlways(NIDO_STATUS, 0, STATUS_MAXHEALTH, 5)// Remove Omega Buff
TNT1 A 0 A_GiveInventory("NidoFloatJump", 1)// Add Beta Floating
TNT1 A 0 A_TakeInventory("NidoOmegaWep", 1)
TNT1 A 0 A_TakeInventory("NidoAlphaWep", 1)
stop
End:
TNT1 A 0
fail
}
}

actor OmegaPickup : CustomInventory 11012
{
//$Category NidoForce-Weapons
Inventory.pickupmessage "Omega ~ Flash"
Inventory.pickupsound "pickup/weapon"
Scale 4.0
States
{
Spawn:
ABUP E 1
loop
Pickup:
TNT1 A 0 A_JumpIfInventory("NidoOmegaWep", 1, "End")
TNT1 A 0 A_GiveInventory("NidoOmegaWep", 1)
TNT1 A 0 A_SelectWeapon("NidoOmegaWep")
TNT1 A 0 ACS_ExecuteAlways(NIDO_WEAPON, 0, 2)
TNT1 A 0 ACS_ExecuteAlways(NIDO_STATUS, 0, STATUS_MAXHEALTH, 6)// Add Omega Buff
TNT1 A 0 A_TakeInventory("NidoFloatJump", 1)// Remove Beta Floating
TNT1 A 0 A_TakeInventory("NidoBetaWep", 1)
TNT1 A 0 A_TakeInventory("NidoAlphaWep", 1)
stop
End:
TNT1 A 0
fail
}
}